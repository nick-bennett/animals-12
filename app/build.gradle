apply plugin: 'com.android.application'

static def getProperty(String propertyFile, String propertyName) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(propertyFile))
    return properties.getProperty(propertyName)
}

static def quoted(String value) {
    return "\"$value\""
}

android {

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "edu.cnm.deepdive.animals"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'BASE_URL',
                quoted(getProperty("$projectDir/../../../services/animals.properties", 'base_url'))
        buildConfigField 'String', 'API_KEY',
                quoted(getProperty("$projectDir/../../../services/animals.properties", 'api_key'))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    //noinspection GroovyAssignabilityCheck
    buildFeatures {
        //noinspection GroovyAssignabilityCheck
        viewBinding true
    }
    
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'androidx.fragment:fragment:1.3.0'

    // Basic Android UI components
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Lifecycle components
    def lifecycleVersion = '2.3.0'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // Material Design components
    implementation 'com.google.android.material:material:1.3.0'

    // ReactiveX libraries
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Retrofit dependencies
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'

    // Gson (JSON parser) library
    implementation 'com.google.code.gson:gson:2.8.6'

    // Picasso library
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Testing libraries
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}